(mod (
        MOD_HASH                ; self puzzle hash 
        FEE_PH                  ; puzzle hash that will receive the fee
        FEE_RATE                ; fee rate (0-10000), e.g., 1% is represented as 100
        MAKER_PH                ; maker puzzle hash (both receive and clawback)
        MAKER_PK                ; maker public key used in clawback case
        TAIL_HASH               ; request CAT tail hash
        RATE                    ; number of CAT per XCH, e.g., 1 XCH = 100 CATs, rate = 100000
        partial_coin_amount     ; amount of partial offer coin
        coin_id                 ; coin id of the partial offer coin
        taken_mojos_or_clawback ; amount of XCH mojos taken, or 0 if clawback
        clawback_fee_mojos      ; blockchain fee in mojos, used in clawback case
    )
        (include *standard-cl-23*)
        (include "fns.clsp")

        (c
            (list ASSERT_MY_AMOUNT partial_coin_amount)
            (if (> taken_mojos_or_clawback 0)
                (let 
                    (
                        (new_partial_coin_amount (- partial_coin_amount taken_mojos_or_clawback))
                        (request_cat_mojos (calculate-cat-mojos RATE taken_mojos_or_clawback))
                        (fee_mojos (calculate-fee-mojos FEE_RATE taken_mojos_or_clawback))
                    )

                    (filter-empty
                        ; partial coin settlement conditions
                        (list
                            (list ASSERT_MY_COIN_ID coin_id)
                            (assert-taker-announcement MAKER_PH TAIL_HASH coin_id request_cat_mojos)
                            (create-settlement-coin (- taken_mojos_or_clawback fee_mojos))
                            (list CREATE_COIN FEE_PH fee_mojos) 
                        )
                        ; if new_partial_coin_amount is greater than 0, create a new partial coin
                        (if (> new_partial_coin_amount 0)
                            (list
                                (create-partial-coin MOD_HASH FEE_PH FEE_RATE MAKER_PH MAKER_PK TAIL_HASH RATE new_partial_coin_amount)
                            )
                            ()
                        )
                    )
                )
                (clawback MAKER_PH MAKER_PK partial_coin_amount clawback_fee_mojos)
            )
        )
)